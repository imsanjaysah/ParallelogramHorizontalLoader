apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group 'com.sanjay.parallelogramhorizontalloader'
version '1.0.0'

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.kotlin.srcDirs
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/parallelogramhorizontalloader-release.aar")
            artifact androidSourcesJar
            groupId
            artifactId 'parallelogram-horizontal-loader'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}


def properties = new Properties()
properties.load(new FileInputStream("local.properties"))

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['Production']
    configurations = ['archives']
    // Default: false. Whether to run this as dry-run, without deploying
    dryRun = false
    // Default: false. Whether to override version artifacts already published
    override = false
    // Default: false. Whether version should be auto published after an upload
    publish = false
    pkg {
        repo = 'ParallelogramHorizontalLoader'
        name = 'com.sanjay.parallelogramhorizontalloader'
        description = "A parallelogram horizontal loader indicator library for Android."
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/sanjay4one/ParallelogramHorizontalLoader.git'
        issueTrackerUrl = 'https://github.com/sanjay4one/ParallelogramHorizontalLoader/issues'
        version {
            name = this.version
            desc = "Version ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}
